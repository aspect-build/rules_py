[
    platform(
        name = "{}_{}".format(os, cpu),
        constraint_values = [
            "@platforms//os:" + os,
            "@platforms//cpu:" + cpu,
        ],
        visibility = ["//py/tools:__subpackages__"],
    )
    for os in [
        "linux",
        "macos",
    ]
    for cpu in [
        "aarch64",
        "x86_64",
    ]
]

# yuk, quick hack until someone can elegantly work out how
# to only include one windows arch in the above.
[
    platform(
        name = "{}_{}".format(os, cpu),
        constraint_values = [
            "@platforms//os:" + os,
            "@platforms//cpu:" + cpu,
        ],
        visibility = ["//py/tools:__subpackages__"],
    )
    for os in [
        "windows",
    ]
    for cpu in [
        "x86_64",
    ]
]

LINUX_ARTIFACTS = [
    "//py/tools/unpack_bin:linux",
    "//py/tools/venv_bin:linux",
]

MACOS_ARTIFACTS = [
    "//py/tools/unpack_bin:macos",
    "//py/tools/venv_bin:macos",
]

WINDOWS_ARTIFACTS = [
    "//py/tools/unpack_bin:windows",
    "//py/tools/venv_bin:windows",
]

sh_binary(
    name = "copy_release_artifacts",
    srcs = ["copy_release_artifacts.sh"],
    args = select({
        "@platforms//os:linux": ["$(rlocationpaths {})".format(s) for s in LINUX_ARTIFACTS],
        "@platforms//os:macos": ["$(rlocationpaths {})".format(s) for s in MACOS_ARTIFACTS],
        "@platforms//os:windows": ["$(rlocationpaths {})".format(s) for s in WINDOWS_ARTIFACTS],
    }),
    data = select({
        "@platforms//os:linux": LINUX_ARTIFACTS,
        "@platforms//os:macos": MACOS_ARTIFACTS,
        "@platforms//os:windows": WINDOWS_ARTIFACTS,
    }),
    deps = ["@bazel_tools//tools/bash/runfiles"],
)
