load("@aspect_rules_py//py:defs.bzl", "py_binary", "py_image_layer")
load("@bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("asserts.bzl", "assert_tar_listing")

platform(
    name = "aarch64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)

platform(
    name = "x86_64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

py_binary(
    name = "my_app_bin",
    srcs = ["__main__.py"],
    venv = "images",
    deps = [
        "//cases/oci/py_image_layer/branding",
        "@aspect_rules_py//py/tests/internal-deps/adder",
        "@pypi//colorama",
    ],
)

py_image_layer(
    name = "my_app_layers",
    binary = ":my_app_bin",
)

platform_transition_filegroup(
    name = "platform_layers",
    srcs = [":my_app_layers"],
    target_platform = ":x86_64_linux",
)

# FIXME(Bazel 8+): This listing needs to be updated/
# Contains old style ~ munging.
assert_tar_listing(
    name = "my_app_layers_test",
    actual = [":platform_layers"],
    expected = ":my_app_layers_listing.yaml",
)

oci_image(
    name = "image",
    # This is defined by an oci.pull() call in /MODULE.bazel
    base = "@ubuntu",
    entrypoint = ["/{}/my_app_bin".format(package_name())],
    tars = [":my_app_layers"],
)

platform_transition_filegroup(
    name = "platform_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": ":aarch64_linux",
        "@platforms//cpu:x86_64": ":x86_64_linux",
    }),
)

# To build the image and load it into it into a local runtime:
# $ bazel run //cases/oci/py_image_layer:image_load
# $ docker run --rm gcr.io/oci_python_hello_world:latest
oci_load(
    name = "image_load",
    image = ":platform_image",
    repo_tags = ["gcr.io/oci_python_hello_world:latest"],
)

platform_transition_filegroup(
    name = "amd64_image",
    srcs = [":image"],
    target_platform = ":x86_64_linux",
)

container_structure_test(
    name = "py_image_test",
    configs = ["py_image_test.yaml"],
    image = ":amd64_image",
    platform = "linux/amd64",
)
