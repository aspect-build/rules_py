load("@rules_python//python/pip_install:requirements.bzl", "compile_pip_requirements")
load("@django//:requirements.bzl", "all_whl_requirements_by_package", "requirement")
load("//py:defs.bzl", "py_binary", "py_library", "py_unpacked_wheel", "resolutions")

django_resolutions = resolutions.from_requirements(all_whl_requirements_by_package, requirement)

py_unpacked_wheel(
    name = "django_wheel",
    src = "@django_4_2_4//file",
)

compile_pip_requirements(
    name = "requirements",
    requirements_in = "requirements.in",
    requirements_txt = "requirements.txt",
)

py_library(
    name = "proj",
    srcs = glob(["proj/**/*.py"]),
    imports = ["./proj"],
    # Depend on django, but not at a particular version, any binary/test rules that
    # depend on this (directly or transitively) will need to resolve it to a version
    # of their choosing.
    virtual_deps = ["django"],
)

py_binary(
    name = "manage",
    srcs = ["proj/manage.py"],
    package_collisions = "warning",
    # Resolve django to the "standard" one from our requirements.txt
    resolutions = django_resolutions,
    deps = [
        ":proj",
    ],
)

py_binary(
    name = "manage.override_django",
    srcs = ["proj/manage.py"],
    # Install the dependencies that the pip_parse rule defined as defaults...
    resolutions = django_resolutions.override({
        # ...but replace the resolution of django with a specific wheel fetched by http_file.
        "django": "//py/tests/virtual/django:django_wheel",
    }),
    deps = [":proj"],
)
