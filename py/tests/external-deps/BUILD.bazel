load("@rules_python//python/pip_install:requirements.bzl", "compile_pip_requirements")
load("//py:defs.bzl", "py_binary", "py_library")
load("@python_toolchain//:defs.bzl", "host_platform")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")

compile_pip_requirements(
    name = "requirements",
    requirements_in = "requirements.in",
    requirements_txt = "requirements.txt",
)

py_library(
    name = "lib",
    srcs = ["lib.py"],
    deps = [
        "@pypi_colorama//:wheel",
    ],
)

py_binary(
    name = "main",
    srcs = ["__main__.py"],
    deps = [
        ":lib",
        "//examples/foo",
        "@pypi_django//:wheel",
    ],
)

genrule(
    name = "run_main",
    outs = ["out"],
    cmd = "|".join([
        "$(execpath main)",
        """sed "s#$$(pwd)#(pwd)#" """,
        "sed 's#^.*execroot/aspect_rules_py/external/python_toolchain_%s#(py_toolchain)#'" % host_platform,
        "sed 's#(main, .*)#(main, REDACTED)#'",
    ]) + "> $(execpath out)",
    tools = ["main"],
)

write_source_files(
    name = "test",
    files = {
        "expected": "out",
    },
)
