load("@aspect_bazel_lib//lib:testing.bzl", "assert_contains")
load("//py:defs.bzl", "py_binary", "py_pex_binary")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_binary")
load("@bazel_skylib//rules:build_test.bzl", "build_test")


# Test that both single-file modules (six) and multi-file modules (cowsay) work with py_pex_binary.
py_binary(
    name = "print_modules_bin",
    srcs = ["print_modules.py"],
    data = ["data.txt"],
    deps = [
        "@bazel_tools//tools/python/runfiles",
        "@pypi_cowsay//:pkg",
        "@pypi_six//:pkg",
    ],
)

py_pex_binary(
    name = "print_modules_pex",
    binary = ":print_modules_bin",
    python_interpreter_constraints = [],
)

# PEX_ROOT is set to avoid warning on default user PEX_ROOT not being writable
genrule(
    name = "run_print_modules_pex",
    outs = ["print_modules_pex.out"],
    cmd = "PEX_ROOT=.pex $(execpath print_modules_pex) >$@",
    tools = ["print_modules_pex"],
)

assert_contains(
    name = "test__print_modules_pex",
    actual = "print_modules_pex.out",
    expected = "Mooo!,cowsay-6.1/cowsay/__init__.py,six-1.16.0/six.py",
)

# Cross-compilation to Linux can work.
# TODO(https://github.com/aspect-build/rules_py/issues/625): support arbitrary cross-compilation.

# At least one of these platforms is a cross-compile, no matter where we run.
platform(
    name = "arm64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)
platform(
    name = "amd64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform_transition_binary(
    name = "print_modules_pex_arm64_linux",
    binary = ":print_modules_pex",
    target_platform = ":arm64_linux",
)
platform_transition_binary(
    name = "print_modules_pex_amd64_linux",
    binary = ":print_modules_pex",
    target_platform = ":amd64_linux",
)

build_test(
    name = "cross_builds",
    targets = [
        ":print_modules_pex_arm64_linux",
        ":print_modules_pex_amd64_linux",
    ],
)
